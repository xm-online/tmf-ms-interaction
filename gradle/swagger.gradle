/*
 * Plugin that provides API-first development using OpenAPI-generator to
 * generate Spring-MVC endpoint stubs at compile time from an OpenAPI definition file
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:${openapiPluginDependencyVersion}"
    }
}
apply plugin: "org.openapi.generator"

void createOpenApiGenerateTask(Map apiConfig) {
    String taskName = "openApiGenerate_" + apiConfig.apiSpecFileName
    task(taskName, type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
        generatorName = "spring"
        inputSpec = "$rootDir/src/main/resources/swagger/${apiConfig.apiSpecFileName}".toString()
        outputDir = "$buildDir/openapi".toString()
        apiPackage = "com.icthh.xm.tmf.ms.interaction.web.api.${apiConfig.apiPackageName}"
        modelPackage = "com.icthh.xm.tmf.ms.interaction.web.api.model.${apiConfig.apiPackageName}"
        apiFilesConstrainedTo = [""]
        modelFilesConstrainedTo = [""]
        supportingFilesConstrainedTo = ["ApiUtil.java"]
        configOptions = [delegatePattern: "true", title: "interaction"]
        validateSpec = true
        importMappings = [Problem: "org.zalando.problem.Problem"]
        additionalProperties = [useTags: "true"]
    }
    compileJava.dependsOn(taskName)
}

List<Map> apiConfigMap = [
        [
                apiSpecFileName: 'api_v1.yml',
                apiPackageName: 'v1',
                modelPackageName: 'v1'
        ],
        [
                apiSpecFileName: 'api_v1.1.yml',
                apiPackageName: 'v1_1',
                modelPackageName: 'v1_1'
        ]
]

for (Map apiConfig : apiConfigMap) {
    createOpenApiGenerateTask(apiConfig)
}

sourceSets {
    main {
        java {
            srcDir file("${project.buildDir.path}/openapi/src/main/java")
        }
    }
}